#+TITLE: El√≠as' Emacs Configuration
#+AUTHOR: El√≠as Hauksson

#+PROPERTY: header-args:emacs-lisp :tangle config.el
#+PROPERTY: header-args :results silent

* Bootstrap (init.el)
This code lives in =init.el= and must be executed *before* this Org file is loaded. It is intentionally **not tangled** from this document.

#+begin_quote
‚ö† This code must be copied into =init.el=.
It is shown here for documentation purposes only and is not tangled.
#+end_quote

#+begin_src emacs-lisp :tangle no
;;; init.el -*- lexical-binding: t -*-

;; Disable default package management
(setq package-enable-at-startup nil)

;; Bootstrap straight.el
(defvar bootstrap-version)
(let ((bootstrap-file
       (expand-file-name "straight/repos/straight.el/bootstrap.el"
			 use-emacs-directory))
      (bootstrap-version 7))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
	(url-retrieve-synchronously
	 "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
	 'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

;; Install and load Org
(straight-use-package 'org)

;; Now load the literate config
(org-babel-load-file
 (expand-file-name "config.org" user-emacs-directory))
#+end_src

* Early Init
This section runs **before Emacs fully starts**. Ideal for startup performance tweaks and frame/UI optimizations.
We start by deferring garbage collection to speed up startup.

#+begin_src emacs-lisp :tangle early-init.el
;;; early-init.el -*- lexical-binding: t -*-

(setq gc-cons-percentage 0.6)
#+end_src

Next, we improve throughput for LSP and subprocess communication.

#+begin_src emacs-lisp :tangle early-init.el
(setq read-process-output-max (* 1024 1024)) ;; 1MB
#+end_src

We also prefer loading new compiled files to avoid stale .elc issues.

#+begin_src emacs-lisp :tangle early-init.el
(setq load-prefer-newer t)
#+end_src

Prevent `package.el` from initializing at startup

#+begin_src emacs-lisp :tangle early-init.el
(setq package-enable-at-startup nil)
#+end_src

To prevent frame flicker, we disable the menu-bar, tool-bar, and vertical scroll bars early.

#+begin_src emacs-lisp :tangle early-init.el
(setq default-frame-alist
      '((menu-bar-lines . 0)
	(tool-bar-lines . 0)
	(vertical-scroll-bars . nil)))
#+end_src

Finally, we temporarily disable expensive file handler checks during startup.

#+begin_src emacs-lisp :tangle early-init.el
(defvar file-name-handler-alist-old file-name-handler-alist)
(setq file-name-handler-alist nil)
(add-hook 'emacs-startup-hook
	  (lambda ()
	    (setq file-name-handler-alist file-name-handler-alist-old)))
#+end_src

* Basic Settings
These settings are applied **after startup** to clean the scratch buffer, reset GC, and ensure UTF-8 encoding.
First we prevent startup message and clear the scratch buffer

#+begin_src emacs-lisp
(setq inhibit-startup-message t
      inhibit-startup-echo-area-message t
      inhibit-startup-screen t
      initial-scratch-message nil
      initial-major-mode 'fundamental-mode)
#+end_src

No bell

#+begin_src emacs-lisp
(setq ring-bell-function 'ignore)
#+end_src

  We also reset the GC to a normal value after startup

#+begin_src emacs-lisp
(add-hook 'emacs-startup-hook
          (lambda ()
            (setq gc-cons-percentage 0.1)))
#+end_src

Then we ensure UTF-8 encoding everywhere

#+begin_src emacs-lisp
(prefer-coding-system 'utf-8)
(set-language-environment "UTF-8")
(setq default-input-method nil)
#+end_src

Suppress normal warnings
#+BEGIN_SRC emacs-lisp
(setq warning-minimum-level :error)
#+END_SRC

Finally we add a small message, which displays the startup time in the echo area

#+begin_src emacs-lisp
(add-hook 'emacs-startup-hook
	  (lambda ()
	    (message "Emacs ready in %.2f seconds"
		     (float-time (time-subtract after-init-time before-init-time)))))
#+end_src

* General Editing
Built-In Options

#+BEGIN_SRC emacs-lisp
(delete-selection-mode   t) ;; Replace selected text when yanking
(global-so-long-mode     t) ;; Mitigate performance for long lines
(global-visual-line-mode t) ;; Break lines instead of truncating
(global-auto-revert-mode t) ;; Revert buffers when they change
(setq-default tab-width  4) ;; Smaller tabs
#+END_SRC

Use spaces instead of tabs. But if working on a project that uses tabs, don't mess it up.

#+BEGIN_SRC emacs-lisp
(defun infer-indentation-style ()
  "Default to no tabs, but use tabs if already in project"
  (let ((space-count (how-many "^  " (point-min) (point-max)))
	(tab-count   (how-many "^\t" (point-min) (point-max))))
    (if (> space-count tab-count) (setq-default indent-tabs-mode nil))
    (if (> tab-count space-count) (setq-default indent-tabs-mode t))))

(setq-default indent-tabs-mode nil)
(infer-indentation-style)
#+END_SRC

Make backspace remove whole tab

#+BEGIN_SRC emacs-lisp
(setq backward-delete-char-untabify-method 'hungry)
#+END_SRC

Better Undo/Redo with undo-fu

#+BEGIN_SRC emacs-lisp
(use-package undo-fu
  :straight t
  :bind
  (("C-x u" . undo-fu-only-undo)
   ("C-x r" . undo-fu-only-redo)))
#+END_SRC

* File Management
Emacs normally creates backup files (`file~`) and auto-save files (`#file#`) in the same folder as your documents.
To avoid clutter, we can redirect them to a central folder and add some extra features like timestamps and versioning.

#+begin_src emacs-lisp
;; Base directory for all temporary Emacs files
(defvar user-emacs-temp-dir
  (expand-file-name "tmp/" user-emacs-directory))
(unless (file-directory-p user-emacs-temp-dir)
  (make-directory user-emacs-temp-dir t))
#+end_src

We can now tell Emacs to put **all backup files** in this folder.
We'll also enable **versioned backups** so Emacs keeps several old copies, not just the last one.

#+begin_src emacs-lisp
;; Backup files (~) go here, with versioning
(setq backup-directory-alist `(("." . ,user-emacs-temp-dir))
      backup-by-copying t       ;; copy instead of rename
      delete-old-versions t     ;; clean up old backups automatically
      kept-new-versions 6       ;; keep last 6
      kept-old-versions 2       ;; keep 2 oldest
      version-control t)        ;; use versioned backups
#+end_src

Similarly, we redirect **auto-save-files** to the same folder.
This keeps them out of your project directories and makes Emacs cleaner.

#+begin_src emacs-lisp
;; Auto-save files (#file#) go here
(setq auto-save-file-name-transforms
      `((".*" ,user-emacs-temp-dir t)))

;; Disable lockfiles to avoid .#file clutter
(setq create-lockfiles nil)
#+end_src

* Package Management
Integrate `use-package`.

#+begin_src emacs-lisp
(setq straight-use-package-by-default t)
(require 'use-package)
#+end_src

* Visuals
** Frames & Windows

Add some border around the whole frame

#+begin_src emacs-lisp
(add-to-list 'default-frame-alist '(internal-border-width . 16))
(set-fringe-mode 0)
(set-face-attribute 'fringe nil :background (face-background 'default))
#+end_src

** Programming-Specific
Make the cursor non-blinking and bar style

#+begin_src emacs-lisp
(blink-cursor-mode 0)
(setq-default cursor-type 'bar)
#+end_src

Then colorize all matching parentheses and delimiters

#+begin_src emacs-lisp
(use-package rainbow-delimiters
  :hook ((prog-mode org-mode) . rainbow-delimiters-mode))
(show-paren-mode t)
#+end_src

Also enable line numbers everywhere except in terminals and org mode

#+begin_src emacs-lisp
(global-display-line-numbers-mode t)
(setq-default display-line-numbers-width 3)

(dolist (mode '(org-mode term-mode vterm-mode shell-mode eshell-mode))
  (add-hook (intern (concat (symbol-name mode) "-hook"))
	    (lambda () (display-line-numbers-mode 0))))
#+end_src

** Fonts & Ligatures
Set up Iosevka as the main font for Emacs and enable ligatures for programming modes.

Base font size

#+begin_src emacs-lisp
(defvar my/font-height 130)
#+end_src

Set Iosevka as default and fixed-pitch font

#+begin_src emacs-lisp
(when (member "Iosevka" (font-family-list))
  (set-face-attribute 'default nil :font "Iosevka" :height my/font-height)
  (set-face-attribute 'fixed-pitch nil :family "Iosevka"))
#+end_src

Define common ligatures and enable them for programming

#+begin_src emacs-lisp
(defvar ligature-def '("|||>" "<|||" "<==>" "<!--" "####" "~~>" "***" "||=" "||>"
		       ":::" "::=" "=:=" "===" "==>" "=!=" "=>>" "=<<" "=/=" "!=="
		       "!!." ">=>" ">>=" ">>>" ">>-" ">->" "->>" "-->" "---" "-<<"
		       "<~~" "<~>" "<*>" "<||" "<|>" "<$>" "<==" "<=>" "<=<" "<->"
		       "<--" "<-<" "<<=" "<<-" "<<<" "<+>" "</>" "###" "#_(" "..<"
		       "..." "+++" "/==" "///" "_|_" "www" "&&" "^=" "~~" "~@" "~="
		       "~>" "~-" "**" "*>" "*/" "||" "|}" "|]" "|=" "|>" "|-" "{|"
		       "[|" "]#" "::" ":=" ":>" ":<" "$>" "==" "=>" "!=" "!!" ">:"
		       ">=" ">>" ">-" "-~" "-|" "->" "--" "-<" "<~" "<*" "<|" "<:"
		       "<$" "<=" "<>" "<-" "<<" "<+" "</" "#{" "#[" "#:" "#=" "#!"
		       "##" "#(" "#?" "#_" "%%" ".=" ".-" ".." ".?" "+>" "++" "?:"
		       "?=" "?." "??" ";;" "/*" "/=" "/>" "//" "__" "~~" "(*" "*)"
		       "\\\\" "://"))

(use-package ligature
  :config
  (ligature-set-ligatures 'prog-mode ligature-def)
  (global-ligature-mode t))
#+end_src

** Icons & Emojis

Add nerd-icons and apple emoji font

#+begin_src emacs-lisp
(use-package nerd-icons)

(use-package emojify
  :config
  (when (member "Apple Color Emoji" (font-family-list))
    (set-fontset-font
     t 'symbol (font-spec :family "Apple Color Emoji") nil 'prepend)))
#+end_src

** Color Theme

For the dark theme we install the Nord theme

#+begin_src emacs-lisp
(use-package doom-themes
  :config
  (setq doom-themes-enable-bold t
	doom-themes-enable-italic t))
#+end_src

and for the light theme we use South

#+begin_src emacs-lisp
(use-package south-theme
  :straight (:host github :repo "SophieBosio/south" :branch "main"))
#+end_src

Then we define the default themes

#+begin_src emacs-lisp
(setq custom-safe-themes t)

(defvar my/default-dark-theme 'doom-nord)
(defvar my/default-light-theme 'south)

(defvar my/default-dark-accent-colour "SkyBlue4")
(defvar my/default-light-accent-colour "#D9EDFC")

(load-theme my/default-dark-theme t)
#+end_src

Then we use `auto-dark-emacs` for switching themes with the system theme.

#+begin_src emacs-lisp
(use-package autothemer
  :defer t)

(use-package auto-dark
  :ensure t
  :hook ((auto-dark-dark-mode
	  .
	  (lambda ()
	    (interactive)
	    (progn
	      (custom-set-faces
	       `(eval-sexp-fu-flash
		 ((t (:background
		      ,my/default-dark-accent-colour)))))
	      `(load-theme ,my/default-dark-theme t))))
	 (auto-dark-light-mode
	   .
	   (lambda ()
	     (interactive)
	     (progn
	       (custom-set-faces
		`(eval-sexp-fu-flash
		  ((t (:background
		       ,my/default-light-accent-colour)))))
	       `(load-theme ,my/default-light-theme t)))))
  :custom
  (auto-dark-themes `((,my/default-dark-theme) (,my/default-light-theme)))
  (auto-dark-polling-interval-seconds 5)
  (auto-dark-allow-osascript t)
  :init (auto-dark-mode t))
#+end_src

** Mode Line

Show the column number in mode line

#+begin_src emacs-lisp
(column-number-mode t)
#+end_src

Custom Mode Line

#+begin_src emacs-lisp
(defvar lsp-modeline--code-actions-string nil)

(setq-default mode-line-format
	      '("%e"
		(:propertize " " display (raise +0.4)) ;; Top padding
		(:propertize " " display (raise -0.4)) ;; Bottom padding

		(:propertize "Œª " face font-lock-comment-face)
		mode-line-frame-identification
		mode-line-buffer-identification

		;; Version control info
		(:eval (when-let (vc vc-mode)
			 (list (propertize "  Óú• " 'face 'font-lock-comment-face)
			       (propertize (truncate-string-to-width
					    (substring vc 5) 50=
					    'face 'font-lock-comment-face)))))

		;; Add space to align to the right
		(:eval (propertize
			" " 'display
			`((space :align-to
				 (- (+ right right-fringe right-margin)
				    ,(+ 3
					(string-width (or lsp-modeline--code-actions-string ""))
					(string-width "%4l:3%c")))))))

		;; LSP code actions
		(:eval (or lsp-modeline--code-actions-string ""))

		;; Line and column numbers
		(:propertize "%4l:%c" face mode-line-buffer-id)))
#+end_src

** Text Display Modes

Olivetti is a minor mode for centering text.

#+begin_src emacs-lisp
(use-package olivetti
  :defer t
  :config
  (setq olivetti-style t))
#+end_src

Adaptive Wrap to visually wrap lines.

#+begin_src emacs-lisp
(use-package adaptive-wrap
  :defer t
  :hook (visual-line-mode . adaptive-wrap-prefix-mode))
#+end_src

Focus dims surrounding text.

#+begin_src emacs-lisp
(use-package focus
  :defer t)
#+end_src 

* Buffers & Navigation

Then tidy it up by organising the buffers by major mode

#+begin_src emacs-lisp
(setq ibuffer-saved-filter-groups
      '(("default"
	 ("Programming" (predicate . (derived-mode-p 'prog-mode)))
	 ("Org"         (mode . org-mode)))))

(add-hook 'ibuffer-mode-hook
	  (lambda ()
	    (ibuffer-switch-to-saved-filter-groups "default")))
#+end_src

Finally, remove some columns.

#+begin_src emacs-lisp
(setq ibuffer-formats
      '((mark " " (name 60 -1 :left))))
#+end_src

* Completion
For completing-read we use Vertico

#+begin_src emacs-lisp
(defun my/take-me-home ()
  (interactive)
  (if (looking-back "/" nil)
      (progn (call-interactively 'delete-minibuffer-contents) (insert "~/"))
    (call-interactively 'self-insert-command)))

(use-package vertico
  :defer t
  :bind (:map vertico-map ("~" . my/take-me-home))
  :config
  (vertico-mode)
  (vertico-multiform-mode)
  (setq read-extended-command-predicate 'command-completion-default-include-p
	vertico-count 32
	read-file-name-completion-ignore-case t
	read-buffer-completion-ignore-case t
	completion-ignore-case t))
#+end_src

The using vertico-posframe it appears in a small child frame

#+begin_src emacs-lisp
(use-package vertico-posframe
  :init
  (setq vertico-posframe-parameters '((left-fringe . 12)
				      (right-fringe . 12)
				      (undecorated . nil)))
  :config
  (vertico-posframe-mode 1)
  (setq vertico-posframe-width 96
	vertico-posframe-height vertico-count
	vertico-multiform-commands '((consult-line (:not posframe))
				     (consult-ripgrep (:not posframe)))))
#+end_src

Corfu provides text completion

#+begin_src emacs-lisp
(use-package corfu
  :defer t
  :custom
  (corfu-auto t)
  (corfu-auto-delay 0.1)
  (corfu-auto-prefix 1)
  (corfu-cycle t)
  (corfu-quit-no-match 'seperator)
  :bind (:map corfu-map
	      (" " . corfu-insert-seperator))
  :init
  (global-corfu-mode))

(setq tab-always-indent 'complete)
#+end_src

Orderless for fuzzy completion

#+BEGIN_SRC emacs-lisp
(use-package orderless
  :ensure t
  :config
  (setq completion-styles '(orderless basic partial-completion)
	completion-category-overrides '((file (styles basic partial-completion)))))
#+END_SRC

* Search
First we install some search utilities

#+BEGIN_SRC emacs-lisp
(use-package ripgrep
  :defer t)

(use-package rg
  :defer t)

(use-package ag
  :defer t)

(use-package wgrep
  :defer t)
#+END_SRC

Consult provides search, navigation, and completion functionality.

#+BEGIN_SRC emacs-lisp
(use-package consult
  :bind (
	 ("C-s"     . consult-line)
	 ("C-M-s"   . consult-ripgrep)
	 ("C-x b"   . consult-buffer)
	 ("C-x C-b" . consult-buffer)
	 ("M-g g"   . consult-goto-line)
	 ("M-g t"   . consult-imenu)
	 ("M-g a"   . consult-imenu-multi)))
#+END_SRC

Use imenu-list for better imenu

#+BEGIN_SRC emacs-lisp
(use-package imenu-list
  :defer t
  :bind (("M-g i" . imenu-list-smart-toggle)))
#+END_SRC

Use Marginalia for annotiations in the minibuffer

#+BEGIN_SRC emacs-lisp
(use-package marginalia
  :init
  (marginalia-mode 1))
#+END_SRC

* Org

Org is used for organising notes.

** Visuals

#+begin_src emacs-lisp
(use-package org
  ;; Center text
  :hook (org-mode . olivetti-mode)
  :config
  ;; Set the sizes and fonts for the various headings
  (custom-set-faces
   '(org-document-title ((t (:height 1.6))))
   '(outline-1          ((t (:height 1.25))))
   '(outline-2          ((t (:height 1.2))))
   '(outline-3          ((t (:height 1.2))))
   '(outline-4          ((t (:height 1.2))))
   '(outline-5          ((t (:height 1.2))))
   '(outline-6          ((t (:height 1.2))))
   '(outline-7          ((t (:height 1.2))))
   '(outline-8          ((t (:height 1.2))))
   '(outline-9          ((t (:height 1.2)))))
  ;; Turn off indent mode globally
  (org-indent-mode -1)
  ;; Preview LaTeX fragments by default
  (setq org-startup-with-latex-preview t)
  ;; Increase the size of LaTeX previews
  (plist-put org-format-latex-options :scale 1.35)
  ;; Show all the headings
  (setq org-startup-folded 'content)
  ;; Declutter by adapting indentations and hiding leading starts
  (setq org-hide-leading-stars t
	org-pretty-entities t
	org-ellipsis "  .")
  ;; For source code blocks make it behave better
  (setq org-src-fontify-natively t
	org-src-tab-acts-natively t
	org-edit-src-content-indentation 0)
  ;; Better headers and TODO's
  (setq org-log-done t
	org-auto-align-tags t
	org-tags-column -80
	org-fold-catch-invisible-edits 'show-and-error
	org-special-ctrl-a/e t
	org-insert-heading-respect-content t))
#+end_src

Hide Emphasis Marker

#+begin_src emacs-lisp
(use-package org-appear
  :commands (org-appear-mode)
  :hook     (org-mode . org-appear-mode)
  :config
  (setq org-hide-emphasis-markers t
	org-appear-autoemphasis   t
	org-appear-autolinks      t
	org-appear-autosubmarkers t))
#+end_src

Show inline images by default

#+begin_src emacs-lisp
(setq org-startup-with-inline-images t)
#+end_src

LaTeX Fragtog is like org-appear, but for LaTeX fragments

#+begin_src emacs-lisp
(use-package org-fragtog
  :after org
  :hook (org-mode . org-fragtog-mode))
#+end_src

Nicer Bullets and Checkboxes

#+begin_src emacs-lisp
(use-package org-superstar
  :after org
  :hook (org-mode . org-superstar-mode))
#+end_src

SVG Elements replaces keywords with nice SVG graphics.

#+begin_src emacs-lisp
(defun my/svg-tag-maybe (fn &rest args)
  "Call fn with args unless point is inside an org src block"
  (when (not (org-in-src-block-p))
    (apply fn args)))

(use-package svg-tag-mode
  :after org
  :config
  (defconst date-re "[0-9]\\{4\\}-[0-9]\\{2\\}-[0-9]\\{2\\}")
  (defconst time-re "[0-9]\\{2\\}:[0-9]\\{2\\}")
  (defconst day-re "[A-Za-z]\\{3\\}")
  (defconst day-time-re (format "\\(%s\\)? ?\\(%s\\)?" day-re time-re))

  (defun svg-progress-percent (value)
    (svg-image (svg-lib-concat
		(svg-lib-progress-bar (/ (string-to-number value) 100.0)
				      nil :margin 0 :stroke 2 :radius 3 :padding 2 :width 11)
		(svg-lib-tag (concat value "%")
			     nil :stroke 0 :margin 0)) :ascent 'center))

  (defun svg-progress-count (value)
    (let* ((seq (mapcar #'string-to-number (split-string value "/")))
	   (count (float (car seq)))
	   (total (float (cadr seq))))
      (svg-image (svg-lib-concat
		  (svg-lib-progress-bar (/ count total) nil
					:margin 0 :stroke 2 :radius 3 :padding 2 :width 11)
		  (svg-lib-tag value nil
			       :stroke 0 :margin 0)) :ascent 'center)))

  (setq svg-tag-tags
	`(;; Org tags
	  (":\\([A-Za-z0-9]+\\)" . ((lambda (tag) (my/svg-tag-maybe
						   #'svg-tag-make tag))))
	  (":\\([A-Za-z0-9]+[ \-]\\)" . ((lambda (tag) (my/svg-tag-maybe tag))))

	  ;; Task priority
	  ("\\[#[A-Z]\\]" . ((lambda (tag)
			       (my/svg-tag-maybe
			       #'svg-tag-make tag
			       :face 'org-priority :beg 2 :end -1 :margin 0))))

	  ;; Progress
	  ("\\(\\[[0-9]\\{1,3\\}%\\]\\)" . ((lambda (tag)
					      (my/svg-tag-maybe
					      #'svg-progress-percent (substring tag 1 -2)))))
	  ("\\(\\[[0-9]+/[0-9]+\\]\\)" . ((lambda (tag)
					    (my/svg-tag-maybe
					    #'svg-progress-count (substring tag 1 -1)))))

	  ;; TODO / DONE
	  ("TODO" . ((lambda (tag)
		       (my/svg-tag-maybe
		       #'svg-tag-make "TODO"
		       :face 'org-todo :inverse t :margin 0))))
	  ("DONE" . ((lambda (tag)
		       (my/svg-tag-maybe
		       #'svg-tag-make "DONE"
		       :face 'org-done :margin 0))))

	  ;; Citation of the form [cite:@Knuth:1984]
	  ("\\(\\[cite:@[A-Za-z]+:\\)" . ((lambda (tag)
					    (my/svg-tag-maybe
					    #'svg-tag-make tag
					    :inverse t :beg 7 :end -1 :crop-right t))))
	  ("\\[cite:@[A-Za-z]+:\\([0-9]+\\]\\)" . (lambda (tag)
						    (my/svg-tag-maybe
						    #'svg-tag-make tag
						    :end -1 :crop-left t)))

	  ;; Active date
	  (,(format "\\(<%s>\\)" date-re) .
	   ((lambda (tag)
	      (my/svg-tag-maybe
	      #'svg-tag-make tag
	      :beg 1 :end -1 :margin 0))))
	  (,(format "\\(<%s \\)%s>" date-re day-time-re) .
	   ((lambda (tag)
	      (my/svg-tag-maybe
	      #'svg-tag-make tag
	      :beg 1 :inverse nil :crop-right t :margin 0))))
	  (,(format "<%s \\(%s>\\)" date-re day-time-re) .
	   ((lambda (tag)
	      (my/svg-tag-maybe
	      #'svg-tag-make tag
	      :end -1 :inverse t :crop-left t :margin 0))))

	  ;; Inactive date
	  (,(format "\\(\\[%s\\]\\)" date-re) .
          ((lambda (tag)
	     (my/svg-tag-maybe
             #'svg-tag-make tag
	     :beg 1 :end -1 :margin 0 :face 'org-date))))
         (,(format "\\(\\[%s \\)%s\\]" date-re day-time-re) .
          ((lambda (tag)
	     (my/svg-tag-maybe
             #'svg-tag-make tag
	     :beg 1 :inverse nil :crop-right t :margin 0 :face 'org-date))))
	 (,(format "\\[%s \\(%s\\]\\)" date-re day-time-re) .
	  ((lambda (tag)
	     (my/svg-tag-maybe
	     #'svg-tag-make tag
	     :end -1 :inverse t :crop-left t :margin 0 :face 'org-date)))))))

(add-hook 'org-mode-hook 'svg-tag-mode)
#+end_src

Custom function to prettify tag and other elements

#+begin_src emacs-lisp
(defun my/org-prettify-symbols-compose-predicate (start end _match)
  "Return non-nil if symbol between START and END should be prettified. Disable prettification inside Org src blocks."
  (not (org-in-src-block-p start)))

(defun my/prettify-symbols-setup ()
  (interactive)
  (setq-local prettify-symbols-compose-predicate
	      #'my/org-prettify-symbols-compose-predicate)
  (setq prettify-symbols-alist
	(mapcan (lambda (x)
		  (when (and (consp x) (stringp (car x)))
		    (list x (cons (upcase (car x)) (cdr x)))))
		'(; Greek symbols
		  ("lambda" . ?Œª)
		  ("delta"  . ?Œî)
		  ("gamma"  . ?Œì)
		  ("phi"    . ?œÜ)
		  ("psi"    . ?œà)
					; Org headers
		  ("#+title:"  . ?‚ÄØ)
		  ("#+author:" . ?‚ÄØ)
                  ("#+date:"   . ?‚ÄØ)                
					; Checkboxes
		  ("[ ]" . ?ÔÇñ)
		  ("[X]" . ?ÔÅÜ)
		  ("[-]" . ?ÔìÉ)
					; Misc
                  ("->" . ?‚ûú)
					; Blocks
		  ("#+begin_src"   . ?Óöë) ; Ôîõ
		  ("#+end_src"     . ?Óöë)
		  ("#+begin_QUOTE" . ?‚Äü)
		  ("#+end_QUOTE"   . ?‚Äù)
					; Drawers
		  (":properties:" . ?ÔÄì)
					; Agenda scheduling
		  ("SCHEDULED:"   . ?üïò)
		  ("DEADLINE:"    . ?‚è∞)
					; Agenda tags Ôìç 
		  (":@projects:"  . ?‚òï)
		  (":work:"       . ?üöÄ)
		  (":@inbox:"     . ?‚úâ)
		  (":goal:"       . ?üéØ)
		  (":task:"       . ?üìã)
		  (":@thesis:"    . ?üìù)
		  (":thesis:"     . ?üìù)
		  (":emacs:"      . ?Óò≤)
		  (":learn:"      . ?üå±)
		  (":code:"       . ?üíª)
		  (":fix:"        . ?üõ†)
		  (":bug:"        . ?üö©)
		  (":read:"       . ?üìö)
					; Roam tags
		  ("#+filetags:"  . ?üìé)
		  (":wip:"        . ?üèó)
		  (":ct:"         . ?‚û°)    ; Category Theory
                  (":verb:"       . ?üåê) ; HTTP Requests in Org mode
                  )))
  (prettify-symbols-mode 1))

(add-hook 'org-mode-hook        #'my/prettify-symbols-setup)
(add-hook 'org-agenda-mode-hook #'my/prettify-symbols-setup)
#+end_src
* Programming

This section configure language tooling for programming modes.

** C/C++
We use Eglot together with clangd.

#+BEGIN_SRC emacs-lisp
(use-package eglot
  :defer t
  :hook ((c-mode c++-mode) . eglot-ensure))
#+END_SRC

Register `clangd` as language server for C/C++.

#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'eglot
  (add-to-list 'eglot-server-programs
               '((c-mode c++-mode) . ("clangd"))))
#+END_SRC

Tune flymake to feel responsive

#+BEGIN_SRC emacs-lisp
(setq flymake-no-changes-timeout 0.5)
#+END_SRC

Keybinding to show all diagnostics

#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'eglot
  (define-key eglot-mode-map (kbd "C-c ! !")
              #'flymake-show-buffer-diagnostics))
#+END_SRC

Common LSP actions

#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'eglot
  (define-key eglot-mode-map (kbd "C-c r") #'eglot-rename)
  (define-key eglot-mode-map (kbd "C-c a") #'eglot-code-actions)
  (define-key eglot-mode-map (kbd "C-c d") #'eglot-find-definitions)
  (define-key eglot-mode-map (kbd "C-c i") #'eglot-find-references))
#+END_SRC

Only format on demand (`M-x eglot-format-buffer`) never automatically.

#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'eglot
  (setq eglot-confirm-server-initiated-edits nil
        eglot-autoshutdown t))
#+END_SRC
