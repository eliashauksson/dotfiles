#+TITLE: Elías' Emacs Configuration
#+AUTHOR: Elías Hauksson

#+PROPERTY: header-args:emacs-lisp :tangle config.el
#+PROPERTY: header-args :result silent

* Bootstrap (init.el)

#+BEGIN_SRC emacs-lisp :tangle no
    ;;; init.el -*- lexical-binding: t -*-

  ;; Disable default package management
  (setq package-enable-at-startup nil)

  ;; Bootstrap straight.el
  (defvar bootstrap-version)
  (let ((bootstrap-file
         (expand-file-name "straight/repos/straight.el/bootstrap.el"
  			 user-emacs-directory))
        (bootstrap-version 7))
    (unless (file-exists-p bootstrap-file)
      (with-current-buffer
  	(url-retrieve-synchronously
  	 "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
  	 'silent 'inhibit-cookies)
        (goto-char (point-max))
        (eval-print-last-sexp)))
    (load bootstrap-file nil 'nomessage))

  ;; Install and load Org
  (straight-use-package 'org)

  ;; Now load the literate config
  (org-babel-load-file
   (expand-file-name "config.org" user-emacs-directory))
#+END_SRC

* Early Init (Startup Infrastructure)

Everything in this section tangles to =early-init.el=.
It is evaluated before package loading and exists solely to minimize startup time
and frame initialization overhead.

** Garbage Collection Optimization

Temporarily relax garbage collection limits during startup to reduce pauses,
then restore conservative defaults once initialization is complete.

#+BEGIN_SRC emacs-lisp :tangle early-init.el
  (defvar my/gc-cons-threshold (* 16 1024 1024))
  (defvar my/gc-cons-percentage 0.1)

  (setq gc-cons-threshold most-positive-fixnum
        gc-cons-percentage 0.6)

  (defun my/restore-gc ()
    (setq gc-cons-threshold my/gc-cons-threshold
  	gc-cons-percentage my/gc-cons-percentage))

  (add-hook 'emacs-startup-hook #'my/restore-gc)
#+END_SRC

** Prefer Newer Compiled Files

#+BEGIN_SRC emacs-lisp :tangle early-init.el
  (setq load-prefer-newer t)
#+END_SRC

** Startup UI Suppression

#+BEGIN_SRC emacs-lisp :tangle early-init.el
  (setq inhibit-startup-screen t
        inhibit-startup-echo-area-message user-login-name)

  (setq initial-buffer-choice nil
        inhibit-startup-buffer-menu t
        inhibit-x-resources t)

  (setq inhibit-splash-screen t)
#+END_SRC

** Scratch Buffer Defaults

#+BEGIN_SRC emacs-lisp :tangle early-init.el
  (setq initial-major-mode 'fundamental-mode
        initial-scratch-message nil)
#+END_SRC

** Frame and UI Chrome

#+BEGIN_SRC emacs-lisp :tangle early-init.el
  (push '(menu-bar-lines . 0) default-frame-alist)
  (setq menu-bar-mode nil)

  (push '(tool-bar-lines . 0) default-frame-alist)
  (setq tool-bar-mode nil)

  (push '(vertical-scroll-bars) default-frame-alist)
  (push '(horizontal-scroll-bars) default-frame-alist)

  (setq scroll-bar-mode nil)
  (when (fboundp 'horizontal-scroll-bar-mode)
    (horizontal-scroll-bar-mode -1))

  (when (bound-and-true-p tooltip-mode)
    (tooltip-mode -1))

  (setq use-file-dialog nil)
  (setq use-dialog-box nil)
#+END_SRC

** Disable Built-in Package Manager

#+BEGIN_SRC emacs-lisp :tangle early-init.el
  (setq package-enable-at-startup nil)
  (setq package-quickstart nil)
#+END_SRC

** File Name Handler Deferral

Disable file name handlers during startup to avoid costly remote and archive
checks while loading core libraries.

#+BEGIN_SRC emacs-lisp :tangle early-init.el
  (defvar my/file-name-handler-alist file-name-handler-alist)

  (setq file-name-handler-alist nil)

  (add-hook 'emacs-startup-hook
  	  (lambda ()
  	    (setq file-name-handler-alist my/file-name-handler-alist)))
#+END_SRC

* Core Defaults

Global behavioral decisions that apply uniformly across all buffers,
modes, and workflows.

** Startup Metrics

Emit a concise startup summary in the minibuffer for perfomance regression tracking.

#+BEGIN_SRC emacs-lisp
  (add-hook 'emacs-startup-hook
  	  (lambda ()
  	    (message "Startup: %s | GCs: %d | Files: %d"
  		     (emacs-init-time)
  		     gcs-done
  		     (length load-history))))
#+END_SRC

** Alerts and Cursor

#+BEGIN_SRC emacs-lisp
  (setq ring-bell-function #'ignore)

  (blink-cursor-mode -1)
  (setq-default cursor-type 'bar)
#+END_SRC

** Disable save and lockfiles

Store backups, auto-saves, and lockfiles in a per-directory .save/ folder

#+BEGIN_SRC emacs-lisp
  (defun my/ensure-save-dir ()
    (when buffer-file-name
      (let* ((file-dir (file-name-directory buffer-file-name))
             (save-dir (expand-file-name ".save/" file-dir)))
        (unless (file-directory-p save-dir)
          (make-directory save-dir t)))))

  (add-hook 'before-save-hook #'my/ensure-save-dir)
  (add-hook 'auto-save-hook #'my/ensure-save-dir)

  (setq auto-save-visited-file-name t)

  (setq backup-directory-alist
        '(("." . ".save/")))

  (setq auto-save-file-name-transforms
        '((".*" ".save/\\&" t)))

  (setq lock-file-name-transforms
        '((".*" ".save/\\&" t)))
#+END_SRC

* Editing Defaults

Baseline text-editing behavior independent of programming language,
major mode, or external package.

** Indentation and Whitespace

Standardize indentation to spaces and make trailing whitespace visible
to avoid accidental formatting errors.

#+BEGIN_SRC emacs-lisp
  (setq-default indent-tabs-mode nil
                tab-width 4
                show-trailling-whitespace t)
#+END_SRC

** Parentheses and Structure

#+BEGIN_SRC emacs-lisp
  (show-paren-mode 1)
  (setq show-paren-delay 0)
#+END_SRC

* Navigation and Buffers

Built-in navigation facilities for buffers, scrolling, and positional awareness.
No third-party packages are introduced here.

** Line Numbers

#+BEGIN_SRC emacs-lisp
  (add-hook 'prog-mode-hook #'display-line-numbers-mode)
  (add-hook 'org-mode-hook #'display-line-numbers-mode)
#+END_SRC

** Scrolling Behavior

#+BEGIN_SRC emacs-lisp
  (setq scroll-margin 3
        scroll-conservatively 100
        scroll-step 1)
#+END_SRC

** Buffer List

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-x C-b") #'ibuffer)
#+END_SRC

* Package Management

Infrastructure for installing and configuring third-party packages.
No user-facing behavior should be defined in this section.

** use-package Integration

Integrate =use-package= with =straight.el= so all package declarations
are declarative, lazy by default, and reproducible.

#+BEGIN_SRC emacs-lisp
  (eval-when-compile (require 'straight))

  (straight-use-package 'use-package)

  (setq straight-use-package-by-default t
        use-package-verbose t)

  (require 'use-package)
#+END_SRC

* Editing (Packages)
* Completion

Minibuffer completion framework, matching style, and candidate ranking.
This section defines how completion looks and how results are ordered.

** Completion UI (Vertico)

#+BEGIN_SRC emacs-lisp
  (use-package vertico
    :init
    (setq vertico-cycle t)
    (vertico-mode 1))
#+END_SRC

** Completion Matching (Orderless)

#+BEGIN_SRC emacs-lisp
  (use-package orderless
    :config
    (setq completion-styles '(orderless basic)
          completion-category-defaults nil
          completion-category-overrides
          '((file (styles basic partial-completion)))))
#+END_SRC

** Completion Ranking (Prescient)

#+BEGIN_SRC emacs-lisp
  (use-package prescient
    :config
    (prescient-persist-mode 1))
#+END_SRC

** Vertico + Prescient Integration

#+BEGIN_SRC emacs-lisp
  (use-package vertico-prescient
    :after (vertico prescient)
    :config
    (vertico-prescient-mode 1))
#+END_SRC

** Completion Annotations (Marginalia)

#+BEGIN_SRC emacs-lisp
  (use-package marginalia
    :init
    (marginalia-mode 1))
#+END_SRC

** In-Buffer Completion (Corfu)

#+BEGIN_SRC emacs-lisp
  (use-package corfu
    :init
    (global-corfu-mode)
    :custom
    (corfu-auto t)
    (corfu-auto-delay 0.1)
    (corfu-auto-prefix 1)
    (corfu-cycle t)
    (corfu-preselect 'prompt))
#+END_SRC

** Keybinding Discovery (which-key)

#+BEGIN_SRC emacs-lisp
  (use-package which-key
    :defer 1
    :config
    (which-key-mode)
    (setq which-key-idle-delay 0.5
          which-key-seperator " -> "
          which-key-prefix-prefix "+"))
#+END_SRC

* Navigation (Packages)

High-level navigation and search commands built on the completion stack.

** Consult

#+BEGIN_SRC emacs-lisp
  (use-package consult
    :bind (("C-x b" . consult-buffer)
           ("C-s"   . consult-line)
           ("M-y"   . consult-yank-pop)
           ("M-g g" . consult-goto-line)
           ("M-g i" . consult-imenu)
           ("M-g r" . consult-ripgrep)))
#+END_SRC

* Programming

#+BEGIN_SRC emacs-lisp
  (use-package eglot
    :straight t
    :hook ((c-mode c++-mode) . eglot-ensure)
    :config
    (define-prefix-command 'my/eglot-map)
    (define-key eglot-mode-map (kbd "C-c e") 'my/eglot-map)

    (define-key my/eglot-map (kbd "d") #'eglot-find-definition)
    (define-key my/eglot-map (kbd "r") #'eglot-rename)
    (define-key my/eglot-map (kbd "f") #'eglot-find-references)
    (define-key my/eglot-map (kbd "h") #'eglot-hover)
    (define-key my/eglot-map (kbd "=") #'eglot-format-buffer))
#+END_SRC

* Version Control
* Org Mode
* UI and Appearance

Visual presentation of Emacs, including fonts, colors, and rendering features.
This section affects appearance only, not behavior.

** Fonts

#+BEGIN_SRC emacs-lisp
  (set-face-attribute 'default nil
                      :family "Iosevka"
                      :height 130
                      :weight 'regular)
#+END_SRC

** Ligatures

#+BEGIN_SRC emacs-lisp
  (dolist (char/ligature-re
           `((?-  . ,(rx (or (or "-->" "-<<" "->>" "-|" "-~" "-<" "->")
                             (+ "-"))))
             (?/  . ,(rx (or (or "/==" "/=" "/>" "/**" "/*")
                             (+ "/"))))
             (?*  . ,(rx (or (or "*>" "*/")
                             (+ "*"))))
             (?<  . ,(rx (or (or "<<=" "<<-" "<|||" "<==>" "<!--" "<=>" "<||" "<|>"
                                 "<-<" "<==" "<=<" "<-|" "<~>" "<=|" "<~~" "<$>"
                                 "<+>" "</>" "<*>" "<->" "<=" "<|" "<:" "<>"
                                 "<$" "<-" "<~" "<+" "</" "<*")
                             (+ "<"))))
             (?:  . ,(rx (or (or ":?>" "::=" ":>" ":<" ":?" ":=")
                             (+ ":"))))
             (?=  . ,(rx (or (or "=>>" "==>" "=/=" "=!=" "=>" "=:=")
                             (+ "="))))
             (?!  . ,(rx (or (or "!==" "!=")
                             (+ "!"))))
             (?>  . ,(rx (or (or ">>-" ">>=" ">=>" ">]" ">:" ">-" ">=")
                             (+ ">"))))
             (?&  . ,(rx (+ "&")))
             (?|  . ,(rx (or (or "|->" "|||>" "||>" "|=>" "||-" "||=" "|-" "|>"
                                 "|]" "|}" "|=")
                             (+ "|"))))
             (?.  . ,(rx (or (or ".?" ".=" ".-" "..<")
                             (+ "."))))
             (?+  . ,(rx (or "+>"
                             (+ "+"))))
             (?\[ . ,(rx (or "[<" "[|")))
             (?\{ . ,(rx "{|"))
             (?\? . ,(rx (or (or "?." "?=" "?:")
                             (+ "?"))))
             (?#  . ,(rx (or (or "#_(" "#[" "#{" "#=" "#!" "#:" "#_" "#?" "#(")
                             (+ "#"))))
             (?\; . ,(rx (+ ";")))
             (?_  . ,(rx (or "_|_" "__")))
             (?~  . ,(rx (or "~~>" "~~" "~>" "~-" "~@")))
             (?$  . ,(rx "$>"))
             (?^  . ,(rx "^="))
             (?\] . ,(rx "]#"))))
    (let ((char (car char/ligature-re))
          (ligature-re (cdr char/ligature-re)))
      (set-char-table-range composition-function-table char
                            `([,ligature-re 0 font-shape-gstring]))))
#+END_SRC

** Theme

#+BEGIN_SRC emacs-lisp
  (use-package doom-themes
    :straight (:build t)
    :config
    (defvar my/dark-theme 'doom-nord)
    (defvar my/light-theme 'doom-nord-light)

    (defun my/gnome-dark-mode-p ()
      (when (eq system-type 'gnu/linux)
        (let ((res (string-trim
                    (shell-command-to-string
                     "gsettings get org.gnome.desktop.interface color-scheme"))))
          (string= res "'prefer-dark'"))))

    (defun my/apply-system-theme ()
      (mapc #'disable-theme custom-enabled-themes)
      (load-theme (if (my/gnome-dark-mode-p) my/dark-theme my/light-theme) t))

    (add-hook 'after-init-hook #'my/apply-system-theme)

    (doom-themes-visual-bell-config)
    (doom-themes-org-config))
#+END_SRC

** Frame Padding and Fringe

#+BEGIN_SRC emacs-lisp
  (when (display-graphic-p)
    (add-to-list 'default-frame-alist '(internal-border-width . 20))
    (set-fringe-style '(10 . 10)))
#+END_SRC

** Icons

#+BEGIN_SRC emacs-lisp
  (use-package all-the-icons
    :straight t
    :if (display-graphic-p))

  (when (display-graphic-p)
    (unless (member "all-the-icons" (font-family-list))
      (all-the-icons-install-fonts t)))

  (use-package all-the-icons-dired
    :straight t
    :if (display-graphic-p)
    :hook (dired-mode . all-the-icons-dired-mode))

  (use-package all-the-icons-ibuffer
    :straight t
    :if (display-graphic-p)
    :hook (ibuffer-mode . all-the-icons-ibuffer-mode))

  (defun my/vc-branch-icon ()
    (when-let ((vc vc-mode))
      (concat " " (all-the-icons-octicon "git-branch" :height 1.0)
              " " (substring vc 5))))
#+END_SRC

** Mode Line

#+BEGIN_SRC emacs-lisp
  (column-number-mode t)

  (defvar my/lsp-code-actions-string "")

  (setq-default mode-line-format
                '("%e"
                  ;; Top and bottom padding
                  (:propertize " " display (raise +0.4))
                  (:propertize " " display (raise -0.4))

                  ;; Lambda symbol
                  (:propertize "λ " face font-lock-comment-face)

                  ;; Buffer name
                  mode-line-buffer-identification

                  ;; Git branch (VC)
                  (:eval (my/vc-branch-icon))

                  ;; Flexible space to push line/column to the right
                  (:eval (propertize
                          " "
                          'display
                          `((space :align-to
                                   (- (+ right right-fringe right-margin)
                                      ,(+ 3
                                          (string-width (or my/lsp-code-actions-string ""))
                                          (string-width "%4l:%c")))))))

                  ;; LSP code actions placeholder
                  (:eval (or my/lsp-code-actions-string ""))

                  ;; Line and column numbers
                  (:propertize "%4l:%c" face mode-line-buffer-id)))
#+END_SRC

** Parenthesis

#+BEGIN_SRC emacs-lisp
  (use-package rainbow-delimiters
    :hook
    ((prog-mode . rainbow-delimiters-mode)
     (org-src-mode . rainbow-delimiters-mode)))
#+END_SRC

* OS Integration
* Utilities
* Experiments
